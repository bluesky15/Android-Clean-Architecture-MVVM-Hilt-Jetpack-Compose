plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "io.github.pavel.jetpack"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        //Get api_key from local.properties
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def apiKey = properties.getProperty("api_key")
        buildConfigField("String", "API_KEY", apiKey)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "env"

    productFlavors {
        development {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField 'String', 'BASE_URL', '"https://newsapi.org/"'
        }

        production {
            buildConfigField 'String', 'BASE_URL', '"https://newsapi.org/"'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'io.github.pavel.jetpack'
}

dependencies {
    //UI and utilities
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'io.coil-kt:coil-compose:2.4.0'
    implementation 'androidx.navigation:navigation-compose:2.7.0-beta02'
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'

    //Di
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0-alpha01'
    implementation 'com.google.dagger:hilt-android:2.47'
    kapt 'com.google.dagger:hilt-compiler:2.47'

    //Network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11'

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}